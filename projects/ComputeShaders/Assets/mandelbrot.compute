// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;
// 0 xmin
// 1 xmax
// 2 ymin
// 2 ymax
RWStructuredBuffer<float> bounds;
RWStructuredBuffer<float4> colors;



[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    
    float k = 0.0009765625;                         // this is simply 1/1024, used to project 1024x1024 texture space to a 2x2 fractal space
    float dx, dy;
    float p, q;
    float xmin, xmax, ymin, ymax;
    float x, y, xnew, ynew, d = 0;                 // we use double precision variables, to avoid precision limit for a bit longer while going deeper in the fractal
    uint itn = 0;
    xmin = bounds[0];
    xmax = bounds[1];
    ymin = bounds[2];
    ymax = bounds[3];
    dx = xmax-xmin;
    dy = ymax-ymin;
    p = xmin + ((int)id.x) * k * dx;
    q = ymin + ((int)id.y) * k * dy;
    x = p;
    y = q;
    while (itn < 255 && d < 4){                     // the essense of the fractal: in this loop we check how many steps it takes for a point to leave 2x2 fractal area
        xnew = x * x - y * y + p;
        ynew = 2 * x * y + q;
        x = xnew;
        y = ynew;
        d = x * x + y * y;
        itn++;
    }

    float s = (float) itn / 256.0;

    Result[id.xy] = colors[itn]; 
}
